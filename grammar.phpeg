//generate a parser with:
// >pegjs --plugin phpegjs --output parser.php grammar.phpeg

Expression
 = out:(Help / Count / Nextevent / PVPSeason) throwaway:$.* {
  return $out;
 }

Help
= "!help"i _? arg:HelpArg? {
  return ["h"=> ["arg" => $arg]];
}

HelpArg
 = "count"i
 / "nextevent"i

 _ "whitespace"
  = [ \t\n\r]+

At 
 = _? "@" _?
 / _ "at"i _ 
 / _
  
Count
 = slice:Slice At cnt:PVECountSpec lag:ReportLag? {
   return ["c" => [
     "slice" => $slice
     ,"count" => $cnt
     ,"lag" => $lag
   ]];
 }
 / slice:Slice _ t:LagOnlyUpdateType lag:ReportLag {
   return ["c" => [
     "slice" => $slice
     ,"count" => $t
     ,"lag" => $lag
   ]];
 }

 PVPSeason
 = slice:PVPSlice At cnt:PVPCountSpec lag:ReportLag? {
   return ["pvp" => [
     "slice" => $slice
     ,"count" => $cnt
     ,"lag" => $lag
   ]];
 }
 /  slice:PVPSlice _ t:LagOnlyUpdateType lag:ReportLag {
   return ["pvp" => [
     "slice" => $slice
     ,"count" => $t
     ,"lag" => $lag
   ]];
 }

LagOnlyUpdateType
 = "last flip"i {return "flip-update";}
 / "last"i {return "lag-update";}


Slice
 = $([12345] "." [7689])


 PVPSlice
  = "pvp"i _ slicepart:("cl"i [6789]) {return $slicepart[0] . $slicepart[1];}
 
PVECountSpec
= dg:Digits & { return $dg > 0 && $dg < 1000; } {
   return $dg;
 } 
/ ("flipped"i / "flip"i) {
  return "flip";
}

PVPCountSpec
 = dg:Digits & { return $dg > 0 && $dg < 5000; } {
   return $dg;
 } 
/ ("flipped"i / "flip"i) {
  return "flip";
}

Digits
 = [0-9]+ {
   $raw = (int) filter_var($this->text(), FILTER_SANITIZE_NUMBER_INT);
   if ($raw > 0 && $raw < 5000)
   {
     return $raw;
   }
   return NULL;
 }

Minutes
  = dg:Digits _? ("mins"i / "min"i / "m"i) (_ "ago"i)? {
    return $dg . "m";
  }

Hours
 = dg:Digits _? "h"i{
    return $dg . "h";
 }
 
ReportLag 
 =  _ lag:(hm:(Hours _? Minutes
 / Hours _? Digits) /*for omitting the minutes designator*/ {
  return $hm[0] . $hm[2];
 }
 / Hours
 / Minutes){return $lag;}

Nextevent
= "next event"i _ eventnum:Digits {return ["n"=> ["nextevent" => $eventnum]];}
